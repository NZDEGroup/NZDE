Actor Colt45_LS : Weapon 10903
{
 Inventory.Pickupmessage "Colt M1911"
 Weapon.SelectionOrder 1900
 Weapon.SlotNumber 2
 Weapon.KickBack 0
 Weapon.AmmoUse 1
 Weapon.AmmoGive1 8
 Weapon.AmmoGive2 16
 Weapon.AmmoType1 "In_M1911LS"
 Weapon.AmmoType2 "Reserve_M1911LS"
 Radius 1
 Height 1
 +NOAUTOFIRE
 +AMMO_OPTIONAL
 States
 {
  Ready:
    COLT A 0 A_JumpIfInventory("reload",1,"Reload")
	COLT A 0 A_JumpIfInventory("knife",1,"Knife")
    COLT A 1 A_WeaponReady
    Loop
  Deselect:
    COLT A 1 A_Lower
	COLT A 0 A_Lower
	COLT A 0 A_Lower
    Loop
  Select:
    COLT A 1 A_Raise
	COLT A 0 A_Raise
	COLT A 0 A_Raise
    Loop
  Fire:
    COLT A 0 A_JumpIfNoAmmo("Reload")
	COLT A 0 A_ZoomFactor(0.96)
	COLT A 0 A_PlaySound("weapons/colt",1,0.9)
	COLT A 0 A_PlaySound("weapons/colt",0,0.1,0,ATTN_NONE)
	COLT A 0 ACS_ExecuteAlways(253,0,random(-2,2),random(-4,4),0)
	COLT A 0 A_SpawnItemEx("GunSmoke",12,0,16,1,0,1,0,SXF_CLIENTSIDE)
	COLT A 0 A_SpawnItemEx("Casing_Pistol",12,0,16,0,random(1,2),random(2,3),0,SXF_CLIENTSIDE)
	TNT1 A 0 A_Jump(64,3)
    COLT A 1 A_FireBullets(4.4,4.4,-1,5,"GunSmoke")
	TNT1 A 0 A_Jump(256,2)
	COLT A 1 A_RailAttack(5,0,1,none,none,1,12,"GunSmoke")
	//COLT A 1 A_FireCustomMissile("bulletColt45",random(-1,1),1,0,7,1,random(-1,1))
	COLT A 0 A_ZoomFactor(0.99)
	COLT A 1 A_Light2
	COLT A 0 A_ZoomFactor(1.0)
	COLT B 1 A_Light1
	COLT C 1 A_Light0
	COLT DE 1 A_WeaponReady
    COLT CA 3 A_WeaponReady
    Goto Ready
  Reload:
	TNT1 A 0 A_TakeInventory("reload",1)
	TNT1 A 0 A_JumpIfInventory("In_M1911LS",8,"Ready")
	TNT1 A 0 A_JumpIfInventory("Reserve_M1911LS",1,"ReloadLoop")
	Goto Ready
  ReloadLoop:
    COLT A 0 A_JumpIfInventory("In_M1911LS",8,"Reloading")
    COLT A 0 A_JumpIfInventory("Reserve_M1911LS",1,1)
	Goto Reloading
	COLT A 0 A_TakeInventory("Reserve_M1911LS",1)
	COLT A 0 A_GiveInventory("In_M1911LS",1)
	Loop
  Reloading:
	COLT A 1 Offset(0,32)
	COLT A 1 Offset(4,32)
	COLT A 1 Offset(12,32)
	COLT A 1 Offset(24,32)
	COLT F 2 Offset(0,32)
	COLT F 0 ACS_ExecuteAlways(253,0,2,2,0)
	COLT F 4 A_PlaySound("weapons/m19out")
	COLT F 0 ACS_ExecuteAlways(253,0,-4,-4,0)
	COLT FGHF 3 Offset(0,32)
	COLT F 8 Offset(0,32)
	COLT F 0 A_PlaySound("weapons/m19in")
	COLT FIJKF 4 Offset(0,32)
	COLT F 0 ACS_ExecuteAlways(253,0,-14,-8,0)
	COLT F 4 Offset(0,32)
	COLT A 1 Offset(24,32)
	COLT A 1 Offset(12,32)
	COLT A 1 Offset(4,32)
	COLT A 1 Offset(0,32)
	COLT A 0 A_TakeInventory("reload",1)
	Goto Ready
  Knife:
	COLT A 1 Offset(-16,52)
	COLT A 1 Offset(-24,60)
	COLT A 1 Offset(-30,66)
	COLT A 1 Offset(-32,68)
	COLT A 1 Offset(-32,84)
	TNT1 A 0 ACS_ExecuteAlways(253,0,random(0,-2),random(6,12),0)
	KNIF A 1 Offset(0,32)
	KNIF A 1 Offset(-4,32)
	KNIF A 1 Offset(-12,32)
	TNT1 A 0 A_CustomPunch(4,0,0,"KnifeSlash")
	KNIF A 1 Offset(-24,32)
	KNIF B 1 Offset(-40,32)
	KNIF B 1 Offset(-64,32)
	KNIF B 1 Offset(-88,32)
	TNT1 A 0 A_CustomPunch(4,0,0,"KnifePuff")
	KNIF C 1 Offset(-112,32)
	KNIF C 1 Offset(-136,32)
	KNIF C 1 Offset(-160,32)
	TNT1 A 0 A_CustomPunch(6,0,0,"KnifeSlash")
	COLT A 1 Offset(32,80)
	COLT A 1 Offset(32,64)
	COLT A 1 Offset(30,62)
	COLT A 1 Offset(24,56)
	COLT A 1 Offset(16,48)
	COLT A 1 Offset(0,32)
	TNT1 A 0 A_TakeInventory("knife",1)
	Goto Ready
  Spawn:
    COLP A -1
    Stop
 }
}
Actor In_M1911LS : Ammo
{
 Inventory.Amount 1
 Inventory.MaxAmount 8
}
Actor Reserve_M1911LS : Ammo
{
 Inventory.Amount 1
 Inventory.MaxAmount 24
}