Actor QuickGrenade : Weapon 11001
{
   Inventory.PickupMessage "Shouldn't have this but..... okay.. whatever..."
   Weapon.AmmoType "GrenadeAmmo"
   +NOALERT
   States
   {
   Spawn:
      PGRN D -1
      Loop
   Ready: 
      HNDG A 1 A_WeaponReady
      Goto Fire
   Deselect:
      HNDG A 1 A_Lower 
	  HNDG A 0 A_Lower 
	  HNDG A 0 A_Lower 
      Loop
   Select:
      HNDG A 1 A_Raise
	  HNDG A 0 A_Raise
	  HNDG A 0 A_Raise
      Loop
   Fire:
      HNDG BCD 2
      HNDG E 2 A_PlaySound("weapons/grenadepin")
	  HNDG E 0 A_TakeInventory("GrenadeAmmo",1)
      HNDG FGHH 2
      HNDG I 2 A_PlaySound("weapons/grenadethr") 
      HNDG J 0 A_FireCustomMissile("ThrownGrenade",0,1,0,2)
      HNDG J 0 A_PlaySound("weapons/grenadethr")
      HNDG JKLMNOPQ 2
      NULL A 12 A_CheckReload
      HNDG XWVUTS 1
      Goto Ready
   }
}

Actor ThrownGrenade
{
 Radius 3
 Height 3
 Speed 13
 Damage 0
 Scale 0.2
 Gravity 0.2
 BounceFactor 0.4
 SeeSound "weapons/grenadebou"
 DamageType "grenade"
 ReactionTime 4
   
 PROJECTILE
 -NOGRAVITY
 +RANDOMIZE
 +HEXENBOUNCE
 +FORCERADIUSDMG
 +BOUNCEONACTORS
 States
 {
  Spawn:
	HGRN A 0 A_CountDown
    HGRN ABCDEF 4 A_SpawnItemEx("GunSmoke_Smoke",0,0,0,0,0,0,0)
    Loop
  Death2:
    TNT1 A 0 A_PlaySound("sounds/molotovexplode")
	TNT1 A 0 A_PlaySound("sounds/molotovdistant",0,0.2,0,ATTN_NONE)
    TNT1 A 0 A_Explode(320,100)
	TNT1 AAA 0 A_SpawnItemEx("M67Grenade_Smoke",0,0,0,random(-1,1),random(-1,1),random(1,2))
	TNT1 AAA 0 A_SpawnItemEx("M67Grenade_Smoke",0,0,0,random(-1,1),random(-1,1),random(1,2),128)
	TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("MolotovGas",0,0,0,random(-4,4),random(-4,4),random(0,4))
	TNT1 A 0 A_SpawnItemEx("MolotovBigFlame",0,0,0,0,0,0,0)
	TNT1 A 0 ACS_ExecuteAlways(270,0,0,0,0)
    Stop
  Death:
    HGRN G 0 A_PlaySound("weapons/grenadeexp")
	HGRN G 0 A_PlaySound("distant/grenadeexp",0,1.0,0,ATTN_NONE)
	HGRN G 0 Radius_Quake(2,15,0,5,0)
    HGRN G 4 A_Explode(295,150)
	TNT1 AAA 0 A_SpawnItemEx("M67Grenade_Explosion",0,0,0,0,0,random(2,3))
	TNT1 AAA 0 A_SpawnItemEx("M67Grenade_Smoke",0,0,0,0,0,random(2,3))
	TNT1 AAAAAA 0 A_SpawnItemEx("M67Grenade_Smoke",0,0,0,random(-2,2),random(-2,2),random(1,2))
	TNT1 AA 0 A_SpawnItemEx("BarrelExplosion",0,0,0,random(-1,1),random(-1,1),random(0,2),0)
	HGRN GHIJ 1 BRIGHT A_SpawnItemEx("M67Grenade_Explosion",0,0,0,random(-2,2),random(-2,2),random(1,3))
    Stop
 }
}

Actor PanzerExplosion
{
 Radius 1
 Height 1
 DamageType "grenade"
 +NOGRAVITY
 States
 {
  Spawn:
	TNT1 A 0
    TNT1 G 0 A_PlaySound("weapons/grenadeexp")
	TNT1 G 0 A_PlaySound("distant/grenadeexp",0,1.0,0,ATTN_NONE)
	TNT1 G 0 Radius_Quake(2,15,0,5,0)
    TNT1 G 4 A_Explode(325,100)
	TNT1 AAAAA 0 A_SpawnItemEx("M67Grenade_Explosion",0,0,0,0,0,random(-4,4))
	TNT1 AAAAA 0 A_SpawnItemEx("M67Grenade_Smoke",0,0,0,0,0,random(-4,4))
	TNT1 AAAAAAAA 0 A_SpawnItemEx("M67Grenade_Smoke",0,0,0,random(-3,3),random(-3,3),random(-3,3))
	TNT1 AA 0 A_SpawnItemEx("BarrelExplosion",0,0,0,random(-1,1),random(-1,1),random(0,2),0)
	TNT1 AAAAAA 1 A_SpawnItemEx("M67Grenade_Explosion",0,0,0,random(-3,3),random(-3,3),random(-3,3))
	TNT1 A 0 ACS_ExecuteAlways(270,0,0,0,0)
	Stop
 }
}
Actor M67Grenade_Explosion
{
  Radius 1
  Height 1
  Damage 0
  RenderStyle Add
  Scale 0.5
  +NOBLOCKMAP
  -NOGRAVITY
  +LOWGRAVITY
  +BOUNCEONACTORS
  States
  {
   Spawn:
	 EXP1 ABCDEFGHIJ 3 BRIGHT
	 TNT1 A -1
     stop
  }
}
Actor M67Grenade_Smoke
{
  Radius 1
  Height 1
  Damage 0
  Scale 2
  +NOBLOCKMAP
  -NOGRAVITY
  +LOWGRAVITY
  +BOUNCEONACTORS
  States
  {
   Spawn:
	 SMOK ABCEFGHIJLMNOP 3
	 TNT1 A -1
     stop
  }
}